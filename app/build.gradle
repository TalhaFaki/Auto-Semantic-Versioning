plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

ext.versionMajor = 1
ext.versionMinor = 0
ext.versionPatch = 0

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.android.semver"
        minSdk 21
        targetSdk 31
        versionCode generateVersionCode()
        versionName generateVersionName()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

def generateVersionCode() {
    return ext.versionMajor * 10000 + ext.versionMinor * 100 + ext.versionPatch
}

def generateVersionName() {
    String versionName = "${versionMajor}.${versionMinor}.${versionPatch}"
    def versionPropsFile = file('version.properties')
    Properties versionProps = new Properties()

    if (versionPropsFile.canRead()) {
        versionProps.load(new FileInputStream(versionPropsFile))
    } else {
        versionProps['BUILD_NUMBER'] = '0'
    }

    def buildNumber = versionProps['BUILD_NUMBER'].toInteger() + 1
    versionProps['BUILD_NUMBER'] = buildNumber.toString()
    versionProps.store(versionPropsFile.newWriter(), null)

    return "${versionName}.BN-${buildNumber}"
}

dependencies {

    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}